/*
 * This build file was auto generated by running the Gradle 'init' task
 * by 'Philipp' at '08.11.16 16:32' with Gradle 3.1
 *
 * This generated file contains a sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/3.1/userguide/tutorial_java_projects.html
 */

plugins {
    id 'com.github.jacobono.jaxb' version '1.3.5'
}

apply plugin: 'application'
apply plugin: "jacoco"
apply plugin: 'antlr'

compileJava.options.encoding = 'UTF-8'
compileJava.dependsOn(xjc)

sourceSets {
    main {
        java {
            srcDirs = [
                'src/main/java',
                'build/generated-src/jaxb',
                'build/generated-src/antlr/main'
            ]
        }
    }
}

// In this section you declare where to find the dependencies of your project
repositories {
    // Use 'jcenter' for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
    mavenCentral()
    maven {
        url "http://formal.iti.kit.edu/maven2/"
    }
}
dependencies {
    compile 'com.fifesoft:rsyntaxtextarea:2.6.0'
    compile 'org.apache.commons:commons-collections4:4.0'
    compile 'de.tu-dresden.inf.lat.jsexp:jsexp:0.2.2'
    compile 'org.apache.commons:commons-lang3:3.1'
    compile 'commons-io:commons-io:2.5'
    compile 'org.choco-solver:choco-solver:4.0.0'
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'org.fxmisc.richtext:richtextfx:0.6.10'
    compile 'org.antlr:antlr4-runtime:4.6'
    compile 'edu.kit.iti.formal:iec61131lang:0.1.11'
    compile group: 'de.jensd', name: 'fontawesomefx', version: '8.9'
    compile group: 'com.sun.xml.bind', name: 'jaxb-impl', version: '2.2.3-1'

    testCompile "junit:junit:4.12"

    antlr 'org.antlr:antlr4:4.6'

    jaxb 'com.sun.xml.bind:jaxb-xjc:2.2.7-b41'
    jaxb 'com.sun.xml.bind:jaxb-impl:2.2.7-b41'
    jaxb 'javax.xml.bind:jaxb-api:2.2.7'

    testCompile "junit:junit:4.12"
    testCompile "org.testfx:testfx-core:4.0.+"
    testCompile "org.testfx:testfx-junit:4.0.+"
    testCompile "org.mockito:mockito-core:2.4.0"
    testCompile group: 'org.powermock', name: 'powermock-module-junit4', version: '1.7.0RC2'
    testCompile group: 'org.powermock', name: 'powermock-api-mockito2', version: '1.7.0RC2'
}

System.setProperty('javax.xml.accessExternalSchema', 'file')

jacocoTestReport {
    reports {
        csv.enabled true
    }
}

jaxb {
    xjc {
        xsdDir "src/main/resources/fileFormats/"
        destinationDir "build/generated-src/jaxb/"
        args = ["-encoding", "UTF-8"]
    }
}

generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ["-visitor", "-long-messages"]
}

jar {
    manifest {
        mainClassName = "edu.kit.iti.formal.stvs.StvsApplication"
    }
}

test {
    filter {
        includeTestsMatching "edu.kit.iti.formal.stvs.model.*"
        includeTestsMatching "edu.kit.iti.formal.stvs.logic.*"
    }


    useJUnit {
        excludeCategories 'edu.kit.iti.formal.stvs.Performance'
    }

    testLogging {
        showStandardStreams = true
    }
    jacoco {
        append = false
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
        classDumpFile = file("$buildDir/jacoco/classpathdumps")
    }
}


task guiTest(type: Test) {
    filter {
        includeTestsMatching "edu.kit.iti.formal.stvs.view.*"
    }
    useJUnit {
        excludeCategories 'edu.kit.iti.formal.stvs.view.Demo'
    }
    testLogging {
        showStandardStreams = true
    }
}
